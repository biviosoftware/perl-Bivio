# Copyright (c) 2025 bivio Software, Inc.  All Rights Reserved.
config({
    'Bivio::Biz::Model::LoginAttempt' => {
        locked_out_failure_count => 1,
    },
});
my($no_setup) = 1;
my($totp_secret);
my($time_step);
my($totp_code);
my($fallback_codes);
FormModel({
    setup_request => sub {
        return
            if $no_setup;
        unless ($totp_secret) {
            $totp_secret = Type_TOTPSecret()->generate_secret(Model_TOTP()->get_default_algorithm);
            $time_step = Biz_RFC6238()->get_time_step(
                Type_DateTime()->to_unix(Type_DateTime()->now), Model_TOTP()->get_default_period);
            $totp_code = Biz_RFC6238()->compute(
                Model_TOTP()->get_default_algorithm,
                Model_TOTP()->get_default_digits,
                $totp_secret,
                $time_step,
            );
            $fallback_codes = Type_RecoveryCode()->generate_new_codes(
                Model_MFAFallbackCodeList()->get_new_code_count);
        }
        unless (Model_TOTP()->unsafe_load) {
            Model_TOTP()->create($totp_secret, $time_step);
            Model_MFAFallbackCodeList()->create($fallback_codes);
        }
        return;
    },
});
my($is_task) = sub {
    my($expected_task) = @_;
    return sub {
        my($case, $res) = @_;
        return $res->[0]->{task_id} eq $expected_task ? 1 : 0;
    };
};
[
    req_state({
        task => 'USER_DISABLE_TOTP_FORM',
        realm => 'demo',
        user => 'demo',
    }),
    [] => $is_task->('NOT_FOUND'),
    inline_case(sub {$no_setup = 0}),
    [] => sub {
        return req(qw(form_model realm_owner name)) eq 'demo';
    },
    error_case({
        login => 'demo',
    } => {
        'RealmOwner.password' => 'NULL',
    }),
    error_case({
        login => 'demo',
        totp_code => sub {$totp_code},
    } => {
        'RealmOwner.password' => 'NULL',
    }),
    error_case({
        login => 'demo',
        'RealmOwner.password' => 'password',
    } => {
        totp_code => 'INVALID_RECOVERY_OPTIONS',
    }),
    error_case({
        login => 'demo',
        'RealmOwner.password' => 'password',
        totp_code => 123456,
    } => {
        totp_code => 'INVALID_TOTP_CODE',
    }),
    error_case({
        login => 'demo',
        'RealmOwner.password' => 'password',
        fallback_code => 'wrong-recovery-code',
    } => {
        fallback_code => 'INVALID_RECOVERY_CODE',
    }),
    [{
        login => 'demo',
        'RealmOwner.password' => 'wrong-password',
        totp_code => sub {$totp_code},
    }] => $is_task->('locked_out_task'),
    [{
        login => 'demo',
        'RealmOwner.password' => 'password',
        totp_code => sub {$totp_code},
    }] => sub {
        return 0
            if Model_TOTP()->new(req())->unsafe_load;
        return 0
            if Model_MFAFallbackCodeList()->new(req())->load_all->get_result_set_size;
        return 1;
    },
    [{
        login => 'demo',
        'RealmOwner.password' => 'password',
        fallback_code => sub {$fallback_codes->get_element(0)},
    }] => sub {
        return 0
            if Model_TOTP()->new(req())->unsafe_load;
        return 0
            if Model_MFAFallbackCodeList()->new(req())->load_all->get_result_set_size;
        return 1;
    },
];
