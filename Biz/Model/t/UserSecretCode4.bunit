# Copyright (c) 2025 bivio Software, Inc.  All Rights Reserved.
Unit();
my($u) = create_user(random_alpha_string());
req()->set_realm_and_user(undef, undef);
my($types) = [Type_SecretCode()->get_non_zero_list];
my($objects) = {
    map(($_->get_name => model('UserSecretCode')->create({
        Model_UserSecretCode()->REALM_ID_FIELD => $u->get('realm_id'),
        type => $_,
    })), @$types),
};
[
    {
        object => sub {model('UserSecretCode')},
        compute_return => sub {
            my(undef, $actual) = @_;
            b_die('not found')
                unless class()->is_blesser_of($actual->[0]);
            return [$actual->[0]->get('user_secret_code_id')];
        },
    } => [
        unauth_load_by_code_and_type => [
            map((
                [$u->get('realm_id'), $objects->{$_->get_name}->get('code'), $_]
                    => $objects->{$_->get_name}->get('user_secret_code_id'),
            ), @$types),
        ],
    ],
    inline_case(sub {req()->set_realm($u)}),
    {
        object => sub {model('UserSecretCode')},
        compute_return => sub {
            my(undef, $actual) = @_;
            b_die('not found')
                unless class()->is_blesser_of($actual->[0]);
            return [$actual->[0]->get('user_secret_code_id')];
        },
    } => [
        unsafe_load_by_code_and_type => [
            map((
                [$objects->{$_->get_name}->get('code'), $_]
                    => $objects->{$_->get_name}->get('user_secret_code_id'),
            ), @$types),
        ],
    ],
    inline_case(sub {req()->set_realm(undef)}),
    class() => [
        is_valid_cookie_code => [
            map(([$u->get('realm_id'), $objects->{$_->get_name}->get('code')] => 0), @$types),
        ],
    ],
    inline_case(sub {
        $objects->{MFA_RECOVERY}->set_archived;
        b_die('set_archived sanity error')
            unless $objects->{MFA_RECOVERY}->get('status')->eq_archived;
        return;
    }),
    class() => [
        is_valid_cookie_code => [
            [$u->get('realm_id'), $objects->{MFA_RECOVERY}->get('code')] => 1,
        ],
    ],
    map({
        my($t) = $_;
        sub {$objects->{$t->get_name}} => [
            set_used => not_die(),
        ];
    } grep(!$_->eq_mfa_recovery, @$types)),
    inline_case(sub {
        foreach my $o (map($objects->{$_->get_name}, grep(!$_->eq_mfa_recovery, @$types))) {
            b_die('set_used sanity error')
                unless $o->get('status')->eq_used;
        }
        return;
    }),
    map({
        my($t) = $_;
        sub {$objects->{$t->get_name}} => [
            set_archived => DIE(),
        ];
    } grep(!$_->eq_mfa_recovery, @$types)),
    sub {b_debug($objects->{MFA_RECOVERY})} => [
        set_used => DIE(),
    ],
];
