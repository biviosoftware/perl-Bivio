# Copyright (c) 2025 bivio Software, Inc.  All Rights Reserved.
config({
    'Bivio::Biz::Model::LoginAttempt' => {
        locked_out_failure_count => 1,
    },
});
FormModel();
req()->set_realm_and_user(qw(demo demo));
req()->initialize_fully('USER_ENABLE_TOTP_FORM');
my($totp_secret);
my($recovery_codes);
my($time_step);
my($v) = {
    login => 'demo',
    'RealmOwner.password' => 'password',
    recovery_codes => sub {$recovery_codes},
    totp_secret => sub {$totp_secret},
};
my($is_task) = sub {
    my($expected_task) = @_;
    return sub {
        my($case, $res) = @_;
        return $res->[0]->{task_id} eq $expected_task ? 1 : 0;
    };
};
[
    empty_case({
        login => 'demo',
        totp_secret => qr/^[a-zA-Z0-9]+$/,
    }),
    inline_case(sub {
        $totp_secret = req(qw(form_model totp_secret));
        $recovery_codes = req(qw(form_model recovery_codes));
        b_die('unexpected recovery_codes count')
            unless $recovery_codes->as_length == Model_RecoveryCodeList()->get_new_code_count;
        $recovery_codes->do_iterate(sub {
            my($it) = @_;
            b_die('unexpected recovery_code format')
                unless $it =~ /^[a-z-]+$/;
            return 1;
        });
        return;
    }),
    error_case({
        %$v,
        totp_code => 123456,
    } => {
        totp_code => 'INVALID_TOTP_CODE',
    }),
    [{
        %$v,
        totp_code => 123456,
        'RealmOwner.password' => 'wrong-password',
    }] => $is_task->('locked_out_task'),
    inline_case(sub {rollback()}),
    [{
        %$v,
        totp_code => sub {
            $time_step = Biz_RFC6238()->get_time_step(
                Type_DateTime()->to_unix(Type_DateTime()->now), Model_TOTP()->get_default_period);
            return Biz_RFC6238()->compute(
                Model_TOTP()->get_default_algorithm,
                Model_TOTP()->get_default_digits,
                $totp_secret,
                $time_step,
            );
        },
    }] => [{
        'Model.TOTP' => {
            algorithm => Model_TOTP()->get_default_algorithm,
            digits => Model_TOTP()->get_default_digits,
            period => Model_TOTP()->get_default_period,
            secret => sub {$totp_secret},
            last_time_step => sub {$time_step},
        },
    }],
    inline_case(sub {
        my($expected_codes) = $recovery_codes->as_array;
        my($found) = 0;
        Model_RecoveryCodeList()->load_all->do_rows(sub {
            my($it) = @_;
            $found++;
            b_die('unexpected recovery_code')
                unless grep({$it->get('RecoveryCode.code') eq $_} @$expected_codes);
            return 1;
        });
        b_die('expected=', Model_RecoveryCodeList()->get_new_code_count, ' recovery codes, found=', $found)
            unless Model_RecoveryCodeList()->get_new_code_count == $found;
        return;
    }),
    [] => $is_task->('FORBIDDEN'),
];
