# Copyright (c) 2008-2009 bivio Software, Inc.  All Rights Reserved.
# $Id$
test_setup('PetShop', 'groupware');
home_page();
my($vars) = test_template_vars({
    user1_email => generate_local_email('mail_user1'),
    user1_name => 'One Guy',
    user2_email => generate_local_email('mail_user2'),
    user2_name => 'Two Guy',
    forum_email => generate_remote_email('mail_forum'),
    randomizer => random_string(),
});
my($d) = test_use('Type.DateTime');
my($next_date) = $d->now;
my($date) = sub {
    return $d->rfc822($next_date = $d->add_seconds($next_date, 1));
};
my($header) = sub {
    my($label, $who) = @_;
    return
	unless $who;
    return (
	$label,
	($label =~ /return/i || !$vars->{$who . '_name'}
	     ? $vars->{$who . '_email'}
	     : $vars->{$who . '_name'}
	     . ' <' . $vars->{$who . '_email'} . '>'),
    );
};
my($headers) = sub {
    my($from, $to, $ref) = @_;
    return {
	$header->('Return-Path', $from),
	$header->('From', $from),
	$header->('To', $to),
	'Message-Id' => next_message_id(),
	'X-Bivio-Mail-Test' => 1,
	Date => $date->(),
	Subject => ref($ref) ? 'Re: ' . $ref->unsafe_get_header('Subject')
	    : $ref ? "$ref " . random_string() . ' & co'
	    : random_string(),
	ref($ref) ? (References => $ref->unsafe_get_header('Message-Id')) : (),
    };
};
my($send_mail) = sub {
    my($template) = shift;
    my($h) = $headers->(@_);
    %$vars = (%$vars, %$h);
    my($head, $body) = test_template($template) =~ /^(.*?\n)\n(.*)/s;
    %$h = (%$h, split(/\: |\n/, $head));
    my($msg) = send_mail(
	map($h->{$_} =~ /([^<]+\@[^>]+)/, qw(From To)), $h, $body);
    return $msg;
};
my($send_and_verify_mail) = sub {
    my($msg) = $send_mail->(@_);
    verify_local_mail(qr{.}, qr{X-Bivio-Forwarded}, 2);
    my($s) = $msg->unsafe_get_header('Subject');
    $s =~ s/Re: //;
    return ($msg, $s);
};
my($check_for_loop) = sub {
    sleep(10);
    follow_link(qw(^mail_forum$ ^mail$));
    b_die('Found duplicate message')
	if get_table_row('_anon#0', 0)->{''}->get('text')
	eq get_table_row('_anon#0', 1)->{''}->get('text');
    return;
};
my($msg, $subject) = $send_and_verify_mail->(qw(1.eml user1 forum),
					     'Long Thread');
login_as('mail_user1');
my($user1_email) = generate_local_email('mail_user1');
follow_link(qw(^mail_forum$ ^mail$));
my($main_uri) = get_uri();
follow_link($subject);
my($msg_uri) = get_uri();
verify_text(escape_html($subject));
%$vars = (
    %$vars,
    inner_from => 'Joe Blow <joe@blow.com>',
    inner_to => 'Two Shoes <two@shoes.com>',
    inner_subject => 'My Inner Subject',
    inner_html => random_string(),
    inner_plain => random_string(),
);
$send_and_verify_mail->(qw(2.eml user2 forum), $msg);
reload_page();
verify_text(qr{Subject:.*$vars->{inner_subject}});
verify_text($vars->{inner_html});
verify_no_text($vars->{inner_plain});
$send_and_verify_mail->(qw(3.eml user1 forum), $msg);
reload_page();
follow_link('embedded-image');
my($image_uri) = get_uri();
verify_content_type('image/gif');
go_back();
follow_link('new_topic');
my($reply_subject) = 'HUI Reply ' . random_string();
submit_form(Send => {
    topic => $reply_subject,
    text => 'initial message',
    'Attach:#1' => file_field('mail/sort_up.gif'),
});
verify_local_mail(qr{.}, qr{.}, 2);
reload_page();
follow_menu_link('mail');
follow_link($reply_subject);
follow_link('author');
verify_text('initial message');
verify_form({
    topic => "Re: $reply_subject",
});
verify_text(qr{To.*\Q$user1_email}s);
go_back();
follow_link('original');
my($original_uri) = get_uri();
verify_text(qr{message-id:}i);
go_back();
follow_link('reply$');
submit_form(Send => {
    text => 'reply text',
});
verify_text('was sent');
verify_local_mail(qr{.}, qr{.}, 2);
follow_menu_link('mail');
follow_link($reply_subject);
verify_text('reply text');
# Test handling of autoreply messages, verifying that repeat messages from a
# misbehaving autoresponder are ignored
%$vars = (
    %$vars,
    ar_subject => random_string(),
);
$next_date = $d->add_seconds($next_date, 120);
($msg, $subject) = $send_and_verify_mail->(qw(ar.eml user1 forum));
follow_link(qw(^mail_forum$ ^mail$));
follow_link($subject);
verify_text($subject);
$send_mail->(qw(ar.eml user1 forum));
$check_for_loop->();
$send_mail->(qw(ar.eml user1 forum));
$check_for_loop->();
clear_local_mail();

follow_link(qw(^mail_forum$ ^mail$));
follow_link($subject);
follow_link('reply$');
my($off_topic_reply) = 'off topic ' . random_string();
submit_form(Send => {
    topic => $off_topic_reply,
    text => 'reply text',
});
verify_text($off_topic_reply);
verify_text($reply_subject);
verify_local_mail(qr{.}, qr{.}, 2);

# Test handling of other type of loop messages with different bodies
%$vars = (
    %$vars,
    bf_subject => random_string(),
    bf_count => 3,
);
($msg, $subject) = $send_and_verify_mail->(qw(bf1.eml user1 forum));
follow_link(qw(^mail_forum$ ^mail$));
follow_link($subject);
verify_text($subject);
%$vars = (
    %$vars,
    bf_count => 4,
);
$send_mail->(qw(bf2.eml user1 forum));
$check_for_loop->();
# Test spam filtering
%$vars = (
    %$vars,
    spam_subject => random_string(),
);
($msg, $subject) = $send_and_verify_mail->(qw(ham.eml user1 forum));
login_as('mail_user1');
follow_link(qw(^mail_forum$ ^mail$));
follow_link($subject);
verify_text(escape_html($subject));
%$vars = (
    %$vars,
    spam_subject => random_string(),
);
$msg = $send_mail->(qw(spam.eml user1 forum));
($subject = $msg->unsafe_get_header('Subject')) =~ s/Re: //;
test_deviance();
verify_local_mail(qr{.}, qr{.});
test_conformance();
follow_link(qw(^mail_forum$ ^mail$));
verify_no_text($subject);
login_as('guest');
test_deviance(qr{forbidden}i);
visit_uri($main_uri);
visit_uri($msg_uri);
visit_uri($original_uri);
visit_uri($image_uri);
test_conformance();
login_as('mail_user2');
visit_uri($main_uri);
visit_uri($msg_uri);
verify_no_link('original');
test_deviance(qr{forbidden}i);
visit_uri($original_uri);
