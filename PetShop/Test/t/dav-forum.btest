# Copyright (c) 2005 bivio Software, Inc.  All Rights Reserved.
# $Id$
test_setup('PetShop');
home_page();
my($root) = '/dav';
basic_authorization('demo');
send_request(PROPFIND => $root);
my($root_forums) = "$root/Forums.csv";
verify_no_text($root_forums);
test_deviance(qr{forbidden}i);
send_request(GET => $root_forums);
test_conformance();
basic_authorization('root');
send_request(PROPFIND => $root);
verify_text($root_forums);
send_request(GET => $root_forums);
#TODO: NEED TO DELETE THE FORUM so we don't get a proliferation
my($top) = "btest_" . random_string();
my($dn) = "DAV $top BTEST";
send_request(PUT => $root_forums, undef, get_content() . <<"EOF");
$top,$dn
EOF
my($top_uri) = "$root/$top";
send_request(PROPFIND => "$top_uri/Files");
send_request(GET => $root_forums);
my($csv) = get_content();
my($tag) = random_string();
$csv =~ s/$dn/$dn $tag/ || die;
send_request(PUT => $root_forums, undef, $csv);
send_request(GET => $root_forums);
verify_text($tag);
my($users) = [map((generate_local_email("$_-" . random_string()))[0], 0 .. 4)];
my($top_members) = "$top_uri/Members.csv";
send_request(GET => $top_members);
send_request(PUT => $top_members, undef, get_content() . <<"EOF");
$users->[0],0,0
$users->[1],1,1
$users->[2],1,0
EOF
send_request(GET => $top_members);
my($found) = 0;
foreach my $line (split(/\n/, get_content())) {
    my($u, $s, $a) = split(/,/, $line);
    foreach my $i (0..2) {
	next unless $users->[$i] eq $u;
	$found++;
	die($line)
	    unless "$s,$a" eq ($i < 2 ? "$i,$i" : '1,0');
    }
}
die("only found: $found: $csv")
    unless $found == 3;
# Add a child forum
my($top_forums) = "$top_uri/Forums.csv";
send_request(GET => $top_forums);
send_request(PUT => $top_forums, undef, get_content() . <<"EOF");
$top-s1,s1
EOF
# Rename child forum
send_request(GET => $top_forums);
($csv = get_content()) =~ s/^\Q$top-s1,s1,/$top-sub1,sub forum 1,/m || die;
send_request(PUT => $top_forums, undef, $csv);
# Verify existing admin shows up in child forum
my($sub1_members) = "$top_uri/$top-sub1/Members.csv";
send_request(GET => $sub1_members);
get_content() =~ /\Q$users->[1],1,1,/ || die;
# Add an admin at the child forum level
send_request(GET => $sub1_members);
send_request(PUT => $sub1_members, undef, get_content() . <<"EOF");
$users->[3],1,1
EOF
# Verify that new admin appears in top forum
send_request(GET => $top_members);
get_content() =~ /\Q$users->[3],1,0,/ || die;
# Add a member at top forum level
send_request(PUT => $top_members, undef, get_content() . <<"EOF");
$users->[4],0,1
EOF
send_request(GET => $sub1_members);
get_content() =~ /\Q$users->[4],0,1,/ || die;
# Remove a member at child forum level
($csv = get_content()) =~ s/^\Q$users->[4]\E,.+\n//m || die;
send_request(PUT => $sub1_members, undef, $csv);
send_request(GET => $sub1_members);
get_content() !~ /\Q$users->[4],0,1,/ || die;
# Test ignore of empty line in CSV
($csv = get_content()) =~ s/^(\Q$users->[3]\E)/\n$1/m || die;
send_request(PUT => $sub1_members, undef, $csv);
send_request(GET => $sub1_members);
my($lc) = (get_content() =~ tr/\n//);
die unless $lc == 4;  #3 members + header line
# Test ignore of line with spaces in CSV
($csv = get_content()) =~ s/^(\Q$users->[3]\E)/   \n$1/m || die;
Bivio::IO::Alert->info($csv);
send_request(PUT => $sub1_members, undef, $csv);
send_request(GET => $sub1_members);
$lc = (get_content() =~ tr/\n//);
die unless $lc == 4;
# Test ignore of line with commas in CSV
($csv = get_content()) =~ s/^(\Q$users->[3]\E)/,,,\n$1/m || die;
Bivio::IO::Alert->info($csv);
send_request(PUT => $sub1_members, undef, $csv);
send_request(GET => $sub1_members);
$lc = (get_content() =~ tr/\n//);
die unless $lc == 4;
# TODO: Test writing .DS_Store and retrieval(?)
# TODO: Test editing admin or mail recipient property for member
