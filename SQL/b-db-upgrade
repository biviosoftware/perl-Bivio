#!perl -w
# Copyright (c) 1999 bivio, LLC.  All rights reserved.
# $Id$
use strict;
my($VERSION) = sprintf('%d.%02d', q$Revision$ =~ /\d+/g);

=head1 NAME

b-db-upgrade - database upgrade

=head1 SYNOPSIS

b-db-upgrade

=head1 DESCRIPTION

B<b-db-upgrade> database upgrade.
This script is the home for database table or data modifications. It stores
a copy of the current version in the database to prevent rerunning the
script.

To install an upgrade, use the magic argument '_upgrade':

    b-db-upgrade _upgrade

To create a new upgrade script, replace the entire contents of the
_upgrade() method and check in the new version.

There is sometimes a lag between scripts installed on the production
system, so create other methods and "chain" them.

=cut

#=IMPORTS
use Bivio::Agent::Request;
use Bivio::Auth::Realm;
use Bivio::Auth::RealmType;
use Bivio::Biz::Model::DbUpgrade;
use Bivio::Biz::Model::MGFSInstrument;
use Bivio::Biz::Model::RealmOwner;
use Bivio::IO::Config;
use Bivio::ShellUtil;
use Bivio::SQL::Connection;
use Bivio::Type::Date;
use Bivio::Type::DateTime;

#=VARIABLES
$| = 1;

sub main {
    my(@argv) = @_;

    # want to avoid accidentally running this script
    my($method) = @argv;
    _usage("missing or bad method")
	    unless $method && $method  =~ /^_upgrade/;

    my($req) = Bivio::Agent::Request->get_current_or_new();
    my($upgrade) = Bivio::Biz::Model::DbUpgrade->new($req);
    if ($upgrade->unauth_load(version => $VERSION)) {
	_usage("$VERSION already ran");
    }

    # Sometimes there may be multiple upgrades and they need to be installed at
    # different times (on test system).  Probably good to keep this flexibility
    # in the tool for now.
    &{\&{$method}}($req);

    # creates a completion entry in db to prevent re-running
    $upgrade->create({
	version => $VERSION,
	run_date_time => Bivio::Type::DateTime->now
    });
    Bivio::SQL::Connection->commit;

    return 1;
}

main(@ARGV);

#=PRIVATE METHODS

# _add_column(string table, string column, string sql_type, string value)
#
# Adds a column to the database and sets the default value.  Add constraints or
# indices, add them after this call.
#
sub _add_column {
    my($table, $column, $type, $value) = @_;
    print "Adding $table.$column\n";
    _do_sql("
ALTER TABLE $table
    ADD $column $type
/
");
    return unless defined($value);

    # Insert default values
    print "Inserting default into $table.$column\n";
    _do_sql("
update $table $column = $value
/");

    return;
}

# _do_sql(string sql) : statement
#
# Returns the last statement executed.
#
sub _do_sql {
    my($sql) = @_;
    my($statement);
    my($s) = '';
    foreach my $line (split(/\n/, $sql)) {
	# Skip comments and blanks
	next if $line =~ /^\s*--|^\s*$/;

	# Execute statement if '/' found
	if ($line =~ /^\s*\/\s*$/) {
	    $statement = Bivio::SQL::Connection->execute($s);
	    $s = '';
	    next;
	}

	# Build up statement
	$s .= $line."\n";
    }
    die("$s: left over statement") if $s;
    return $statement;
}

# _drop_column(string table, string column)
#
# Drops the column.
#
sub _drop_column {
    my($table, $column) = @_;
    print "Dropping $table.$column\n";
    _do_sql("
ALTER TABLE $table
    DROP COLUMN $column
/
");
    return;
}

# _drop_constraints(string table, string column) : int
#
# Drop all the constraints on a particular table and column.
# Returns number of constraints dropped.
#
sub _drop_constraints {
    my($table, $column) = map {uc($_)} @_;
    # Find all constraints on the table
    my($statement) = _do_sql(<<"EOF");
	    SELECT user_cons_columns.constraint_name
	      FROM user_cons_columns, user_constraints
	      WHERE user_cons_columns.column_name = '$column'
	      AND user_cons_columns.table_name = '$table'
	      AND user_constraints.constraint_name
	      = user_cons_columns.constraint_name
              /
EOF
    my(@constraints);
    while (my($name) = $statement->fetchrow_array) {
	push(@constraints, $name);
    }
    $statement->finish;

    # Drop the constraints
    foreach my $c (@constraints) {
	_do_sql("ALTER TABLE $table drop constraint $c
                /");
    }
    return int(@constraints);
}

# _fix_mgfs_names(hash_ref name_map)
#
# Takes a map of (bad_name => 'good_name') and updates mgfs_instrument_t
# and instrument_t.
#
sub _fix_mgfs_names {
    my($name_map) = @_;

    my($mgfs_instrument) = Bivio::Biz::Model::MGFSInstrument->new(
	    Bivio::Agent::Request->get_current_or_new);
    foreach my $bad_name (keys(%$name_map)) {
	if ($mgfs_instrument->unsafe_load(name => $bad_name)) {

	    # this will all synchronize with the instrument_t table
	    $mgfs_instrument->update({name => $name_map->{$bad_name}});
	}
	else {
	    print(STDERR "\nmgfs_instrument name '$bad_name' not found\n");
	}
    }
    return;
}

# _modify_column(string table, string column, string type)
#
# Change the type of the column.
#
sub _modify_column {
    my($table, $column, $type) = @_;
    _do_sql("
ALTER TABLE $table MODIFY $column $type
/");
    return;
}

# _sqlplus(string db_login, string actions) : string_ref
#
# Runs I<actions> in sqlplus with I<db_login>.
# Returns result of the execution.
#
# I<actions> will have a semicolon (;) appended to it.
#
# If any of I<actions> fails, will rollback and exit.
#
sub _sqlplus {
    my($db_login, $actions) = @_;
    my($res) = Bivio::ShellUtil::piped_exec(undef, 'su - oracle -c sqlplus',
            <<"EOF");
$db_login
set linesize 10000
set pagesize 10000
whenever sqlerror exit sql.sqlcode rollback
whenever oserror exit sql.sqlcode rollback
$actions;
disconnect
exit
EOF
    return $res;
}

# _upgrade(Bivio::Agent::Request req)
#
# Replace this method with the table/data manipulations.
#
sub _upgrade {
    my($req) = @_;

    use Bivio::Auth::Realm;
    use Bivio::Biz::Model::ExpenseCategory;
    use Bivio::Biz::Model::RealmOwner;
    use Bivio::Data::EW::ClubImporter;

    _upgrade_alter_tables();

    _upgrade_expenses($req);

    print("\n*** done ***\n");
    return;
}

# _upgrade_alter_tables()
#
# Adds and alters existings tables
#
sub _upgrade_alter_tables {
    # create expense_category_t and expense_info_t
    print("adding expense_category_t and expense_info_t\n");
    _do_sql('
CREATE TABLE expense_category_t (
  expense_category_id NUMBER(18),
  realm_id NUMBER(18),
  name VARCHAR2(100),
  deductible NUMBER(1),
  parent_category_id NUMBER(18)
)
  tablespace data_1m
  pctfree 5 pctused 90 initrans 2 storage (freelists 8)
/
ALTER TABLE expense_category_t
  add constraint expense_category_t1
  primary key(expense_category_id)
  using index tablespace index_1m pctfree 0 initrans 4 storage (freelists 8)
/
ALTER TABLE expense_category_t
  add constraint expense_category_t2
  foreign key (realm_id)
  references realm_owner_t(realm_id)
/
ALTER TABLE expense_category_t
  add constraint expense_category_t3
  foreign key (parent_category_id)
  references expense_category_t(expense_category_id)
/
ALTER TABLE expense_category_t modify realm_id not null
/
ALTER TABLE expense_category_t modify name not null
/
ALTER TABLE expense_category_t modify deductible not null
/
ALTER TABLE expense_category_t
  add constraint expense_category_t4
  check (deductible between 0 and 1)
/
CREATE INDEX expense_category_t5 on expense_category_t (
  realm_id
)
  tablespace index_1m
  pctfree 0 initrans 4 storage (freelists 8)
/
CREATE INDEX expense_category_t6 on expense_category_t (
  parent_category_id
)
  tablespace index_1m
  pctfree 0 initrans 4 storage (freelists 8)
/
CREATE sequence expense_category_s
  minvalue 100013 maxvalue 999999999999999999
  nocycle cache 10 increment BY 100000
/
');

    _do_sql('
CREATE TABLE expense_info_t (
  entry_id NUMBER(18),
  expense_category_id NUMBER(18),
  realm_id NUMBER(18),
  allocate_equally NUMBER(1)
)
  tablespace data_1m
  pctfree 5 pctused 90 initrans 2 storage (freelists 8)
/
ALTER TABLE expense_info_t
  add constraint expense_info_t1
  primary key(entry_id)
  using index tablespace index_1m pctfree 0 initrans 4 storage (freelists 8)
/
ALTER TABLE expense_info_t
  add constraint expense_info_t2
  foreign key (entry_id)
  references entry_t(entry_id)
/
ALTER TABLE expense_info_t
  add constraint expense_info_t3
  foreign key (expense_category_id)
  references expense_category_t(expense_category_id)
/
ALTER TABLE expense_info_t
  add constraint expense_info_t4
  foreign key (realm_id)
  references realm_owner_t(realm_id)
/
ALTER TABLE expense_info_t modify expense_category_id not null
/
ALTER TABLE expense_info_t modify realm_id not null
/
ALTER TABLE expense_info_t modify allocate_equally not null
/
ALTER TABLE expense_info_t
  add constraint expense_info_t5
  check (allocate_equally between 0 and 1)
/
CREATE INDEX expense_info_t6 on expense_info_t (
  realm_id
)
  tablespace index_1m
  pctfree 0 initrans 4 storage (freelists 8)
/
');

    # alter tax_category constraints
    print("altering tax_category constraints entry_t, member_allocation_t\n");
    _do_sql('
ALTER TABLE entry_t DROP constraint ENTRY_T9
/
ALTER TABLE member_allocation_t DROP constraint MEMBER_ALLOCATION_T5
/
ALTER TABLE entry_t
  add constraint entry_t9
  check (tax_category between 1 and 11)
/
ALTER TABLE member_allocation_t
  add constraint member_allocation_t5
  check (tax_category between 1 and 11
  or tax_category = 100)
/
');
    return;
}

# _upgrade_expenses(Bivio::Agent::Request req)
#
# Creates default expense categories and expense info values.
#
sub _upgrade_expenses {
    my($req) = @_;
    # create default expense categories
    # classify existing expenses
    print("creating default expense categories and existing expense info\n");
    my($realm_owner) = Bivio::Biz::Model::RealmOwner->new($req);
    my($sth) = Bivio::SQL::Connection->execute('
            SELECT realm_id, name
            FROM realm_owner_t, club_t
            WHERE realm_owner_t.realm_id=club_t.club_id
            ORDER by name',
	    []);
    while (my $row = $sth->fetchrow_arrayref) {
	my($realm_id, $name) = @$row;
	print("\t$name\n");
	$realm_owner->unauth_load(realm_id => $realm_id);
	$req->set_realm(Bivio::Auth::Realm->new($realm_owner));
	Bivio::Biz::Model::ExpenseCategory->new($req)->create_initial;
	Bivio::Data::EW::ClubImporter->default_expense_categories($req);
    }
    return;
}

# _usage(array message)
#
# Print a message and terminate
sub _usage {
    my($msg) = join('', @_);
    print STDERR <<"EOF";
$0: $msg
usage: $0 _upgrade
EOF
    exit(1);
}

=head1 SEE ALSO

Bivio::IO::Config

=head1 COPYRIGHT

Copyright (c) 1999 bivio, LLC.  All rights reserved.

=head1 VERSION

$Id$

=cut

#Local Variables:
#mode:cperl
#End:
