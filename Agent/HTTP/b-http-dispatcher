#!perl -w
# Copyright (c) 1999 bivio, LLC.  All rights reserved.
# $Id$
use strict;

=head1 NAME

b-http-dispatcher - starts Apache running Bivio::Agent::HTTP::Dispatcher

=head1 SYNOPSIS

b-http-dispatcher [--config] [-n] [server-name]

=head1 DESCRIPTION

B<b-http-dispatcher> is used to run L<httpd|httpd> in single user mode
with a dynamically created configuration file.

Logs are written to stdout.

=head1 OPTIONS

=over 4

=item -n

Don't execute, just show configuration file generated and
command invocation string.

=item --[server-name.]port=(number|name)

The port to use (required in config file or on command line).
NOTE: if you are using the configuration generated by
L<b-societas-start|b-societas-start>, you will need to
use B<--http.port=(number|name)> form.

=item --[server-name.]handler=package

The package name of the handler to use.  Defaults to
L<Bivio::Agent::HTTP::Dispatcher|Bivio::Agent::HTTP::Dispatcher>.

=back

=cut

#=IMPORTS
use Bivio::IO::Config;
use Bivio::IO::Alert;
use Cwd ();

#=VARIABLES
my($_APACHE_HOME) = '/usr/local/apache';
my($_HTTPD) = $_APACHE_HOME . '/bin/httpd';
Bivio::IO::Config->register({
    Bivio::IO::Config->NAMED => {
	'port' => Bivio::IO::Config->REQUIRED,
	'handler' => 'Bivio::Agent::HTTP::Dispatcher',
    },
});

sub main {
    my(@argv) = @_;
    Bivio::IO::Config->initialize(\@argv);
    my($execute) = 1;
    my($server_name) = undef;
    local($_);
    while (@argv) {
	$_ = shift(@argv);
	/^-n/ && ($execute = 0, next);
	/^-/ && &_usage("unknown option \"$_\"");
	defined($server_name) && &_usage('too many arguments');
	$server_name = $_;
    }
    my($cfg) = Bivio::IO::Config->get($server_name);
    if ($ENV{PERLLIB}) {
	my($httpd) = $ENV{PERLLIB} . '../external/apache/src/httpd';
	-x $httpd && ($_HTTPD = $httpd);
    }
    if ($execute) {
	system('rm -f httpd.lock.* httpd.pid httpd[0-9]*.conf');
	system('cvs checkout html; cd html/societas; make INSTALLDIR=../')
		unless -f 'html/index.html';
	&symlink("$_APACHE_HOME/libexec", 'libexec');
	&symlink('.', 'logs');
    }
    else {
	print <<"EOF";
rm -f httpd.lock.* httpd.pid httpd[0-9]*.conf
cvs checkout html/i
ln -s $_APACHE_HOME/libexec libexec
ln -s . logs
EOF
    }
    my($port) = $cfg->{port};
    my($user) = getpwuid($>) || $>;
    my($group) = getgrgid($)) || $);
    my($pwd) = &Cwd::cwd();
    my($host) = `hostname`;
    my($handler) = $cfg->{handler};
    my($bivio_conf) = $ENV{'BIVIO_CONF'}
	    ? "PerlSetEnv BIVIO_CONF $ENV{'BIVIO_CONF'}" : '';
    chop($host);
    $host = sprintf("%d.%d.%d.%d", unpack('C4', (gethostbyname($host))[4]));
    local($_);
    my($conf) = $execute ? "httpd$$.conf" : "&STDOUT";
    open(OUT, ">$conf") || die("open $conf: $!");
    while (<DATA>) {
	# Yup, want an extra "e" to get double interpolation.  Kewl, huh?
	s/<(\$\w+)>/$1/eeg;
    }
    continue {
	(print OUT) || die("write $conf: $!");
    }
    close(OUT) || die("close $conf: $!");
    close(DATA);
    if ($execute) {
	print STDERR "Starting: $_HTTPD -X -d $pwd -f $pwd/$conf\n";
	exec("$_HTTPD", '-X', '-d', $pwd, '-f', "$pwd/$conf");
	die("$_HTTPD: $!");
    }
    else {
	print "Would start: $_HTTPD -X -d $pwd -f $pwd/$conf\n";
    }
}

sub symlink {
    my($file, $link) = @_;
    -e $link || symlink($file, $link) || die("symlink($file, $link): $!");
}

# We don't do dynamic reconfiguration
sub handle_config {
}

sub _usage {
    my($msg) = join('', @_);
    print STDERR <<"EOF";
$0: $msg
usage: $0 [--config] [-n] [server-name]
EOF
    exit(1);
}

&main(@ARGV);

=head1 ENVIRONMENT

=over 4

=item $PERLLIB

Must point to the appropriate development directory if you are
starting the server for testing your own copy.  See L<"FILES">.

=back

=head1 FILES

=over 4

=item html/index.html

=item html/societas

The command "cvs checkout html/societas" will be executed in the current
directory and the static site will be built,
if the directory doesn't already exist.

=item httpd.lock.*

is removed and rewritten with new pid

=item httpd.pid

is removed and rewritten with new pid

=item httpd[0-9]*.conf

is removed and rewritten with new pid

=item libexec

is linked to F</usr/local/apache/libexec> if it already doesn't exist.

=item $PERLLIB/../external/apache/src/httpd

binary to be used if it exists and is executable

=back

=head1 SEE ALSO

Bivio::IO::Config, Bivio::Agent::HTTP::Dispatcher, httpd, mod_perl

=head1 COPYRIGHT

Copyright (c) 1999 bivio, LLC.  All rights reserved.

=head1 VERSION

$Id$

=cut

#Local Variables:
#mode:cperl
#End:
__DATA__
#
# This file was dynamically generated by <$0>
#
ResourceConfig /dev/null
AccessConfig /dev/null

ServerName <$host>
Listen <$port>
User <$user>
Group <$group>
ServerAdmin <$user>

PerlWarn on
# Can't be on and use PERLLIB.
PerlFreshRestart off
<$bivio_conf>
PerlModule <$handler>
#RJN: This doesn't work for some reason
PassEnv ORACLE_HOME
PassEnv DBI_USER
PassEnv DBI_PASS
PassEnv ORACLE_SID
PerlPassEnv ORACLE_HOME
PerlPassEnv DBI_USER
PerlPassEnv DBI_PASS
PerlPassEnv ORACLE_SID

Timeout 10
HostnameLookups off
KeepAlive off
MinSpareServers 1
MaxSpareServers 1
StartServers 1
MaxClients 2
MaxRequestsPerChild 1000
LimitRequestBody 4194304

ServerRoot <$pwd>
DocumentRoot <$pwd>/html
PidFile httpd.pid
ErrorLog |cat
# Possible values include: debug, info, notice, warn, error, crit,
# alert, emerg.
LogLevel debug
LogFormat "%h %l %u %t \"%r\" %>s %b \"%{Referer}i\" \"%{User-Agent}i\"" combined
CustomLog |cat combined
TypesConfig /etc/mime.types
DefaultType text/html
LockFile httpd.lock

<Directory />
    AllowOverride None
    Options FollowSymLinks
</Directory>

<Location /modperl-status>
    SetHandler perl-script
    PerlHandler Apache::Status
</Location>

<LocationMatch "^/[a-zA-Z0-9_]{3,}($|/)">
    AuthName bivio:<$port>
    AuthType Basic
    SetHandler perl-script
    PerlHandler <$handler>
</LocationMatch>

BrowserMatch "Mozilla/2" nokeepalive
BrowserMatch "MSIE 4\.0b2;" nokeepalive downgrade-1.0 force-response-1.0
BrowserMatch "RealPlayer 4\.0" force-response-1.0
BrowserMatch "Java/1\.0" force-response-1.0
BrowserMatch "JDK/1\.0" force-response-1.0
